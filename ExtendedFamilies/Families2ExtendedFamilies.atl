-- INFOY080 - Software Architectures
-- Assignment 4 - Dehouck Samuel & Delhaye Quentin
-- @path Families=/ExtendedFamilies/Families.ecore
-- @path ExtendedFamilies=/ExtendedFamilies/ExtendedFamilies.ecore
module Families2ExtendedFamilies;

create OUT: ExtendedFamilies from IN: Families;


helper context Families!Member def: isFemale: Boolean =
	if not self.familyMother.oclIsUndefined() then
		true
	else
		not self.familyDaughter.oclIsUndefined()
	endif;

helper context Families!Member def: isParent: Boolean =
	if not self.familyMother.oclIsUndefined() then
		true
	else
		not self.familyFather.oclIsUndefined()
	endif;

helper context Families!Member def: isMother: Boolean =
	self.isFemale and self.isParent;

helper context Families!Member def: isFather: Boolean =
	not self.isFemale and self.isParent;

helper context Families!Member def: isDaughter: Boolean =
	self.isFemale and not self.isParent;

helper context Families!Member def: isSon: Boolean =
	not self.isFemale and not self.isParent;

abstract rule Member2Person {
	from
		s: Families!Member
	to
		t: ExtendedFamilies!Person (
			firstName <- s.firstName
		)
}

rule Member2Father extends Member2Person {
	from
		s: Families!Member (s.isFather)
	to
		t: ExtendedFamilies!Male (
			-- The family reference can only be resolved once the reference of
			-- s.familyFather has been set. Once it's done, we use 't_family'
			-- from the rule having resolved the first reference.
			family <- thisModule.resolveTemp(s.familyFather, 't_family')
		)
}

rule Member2Son extends Member2Person {
	from
		s: Families!Member (s.isSon)
	to
		t: ExtendedFamilies!Male (
			family <- thisModule.resolveTemp(s.familySon, 't_family'),
			-- The parent/child relation needs only to be set for one
			-- actor, the other one being set automatically.
			-- We choose to set the reference for the children as they
			-- only have one mother and one father according to the
			-- specification.
			parents <- thisModule.resolveTemp(s.familySon.father, 't'),
			parents <- thisModule.resolveTemp(s.familySon.mother, 't')
		)
}

rule Member2Mother extends Member2Person {
	from
		s: Families!Member (s.isMother)
	to
		t: ExtendedFamilies!Female (
			family <- thisModule.resolveTemp(s.familyMother, 't_family')
		)
}

rule Member2Daughter extends Member2Person {
	from
		s: Families!Member (s.isDaughter)
	to
		t: ExtendedFamilies!Female (
			family <- thisModule.resolveTemp(s.familyDaughter, 't_family'),
			parents <- thisModule.resolveTemp(s.familyDaughter.mother, 't'),
			parents <- thisModule.resolveTemp(s.familyDaughter.father, 't')
		)
}

--- Creates a family to which we'll add persons.
rule Family2Family {
	from
		s_family: Families!Family
	to
		t_family: ExtendedFamilies!Family (
			lastName <- s_family.lastName.toUpper()
		)
}
